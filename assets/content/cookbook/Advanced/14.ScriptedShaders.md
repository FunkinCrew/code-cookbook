[tags]: / "advanced,hscript,shader"

# Scripted Shaders

This chapter will walk you through the process of using a custom runtime shader to alter the appearance of a given sprite or camera using GLSL.

## Setup

To get started, let's set up a fragment shader, with the `.frag` extension, in the `mods/mymod/shaders` folder. For example, let's set up a simple hue shift shader titled `hueShift.frag`:

```glsl
#pragma header

// https://gist.github.com/983/e170a24ae8eba2cd174f

uniform float hue;

vec3 rgb2hsv(vec3 c)
{
    vec4 K = vec4(0.0, -1.0 / 3.0, 2.0 / 3.0, -1.0);
    vec4 p = mix(vec4(c.bg, K.wz), vec4(c.gb, K.xy), step(c.b, c.g));
    vec4 q = mix(vec4(p.xyw, c.r), vec4(c.r, p.yzx), step(p.x, c.r));
    float d = q.x - min(q.w, q.y);
    float e = 1.0e-10;
    return vec3(abs(q.z + (q.w - q.y) / (6.0 * d + e)), d / (q.x + e), q.x);
}

vec3 hsv2rgb(vec3 c)
{
    vec4 K = vec4(1.0, 2.0 / 3.0, 1.0 / 3.0, 3.0);
    vec3 p = abs(fract(c.xxx + K.xyz) * 6.0 - K.www);
    return c.z * mix(K.xxx, clamp(p - K.xxx, 0.0, 1.0), c.y);
}

void main() {
  vec4 col = flixel_texture2D(bitmap, openfl_TextureCoordv);
  vec3 hsv = rgb2hsv(col.rgb);
  hsv.x += hue;
  gl_FragColor = vec4(hsv2rgb(hsv), col.a);
}
```

## Loading and Applying the Shader

You can register a variable to hold your shader using `FlxRuntimeShader`. Here's an example applying the shader to the `boyfriend` sprite in a stage script:

Shader application is most common in stage scripts, but you can also apply them in other scripts as needed.

```haxe
import funkin.play.stage.Stage;
import flixel.addons.display.FlxRuntimeShader;
import funkin.play.PlayState;

class TemplateStage extends Stage
{
  function new()
  {
    super('template');
  }

  var hueShader:FlxRuntimeShader;

  function onCreate(event:ScriptEvent):Void
  {
    super.onCreate(event);

    hueShader = new FlxRuntimeShader(Assets.getText(Paths.frag('hueShift')));
    PlayState.instance.currentStage.getBoyfriend().shader = hueShader;
  }
}
```

A shader is registered, but it doesn't do anything yet. To see the effect, you'll need to update the shader's uniform values.

```haxe
// Set the hue shift value (in this case, shifting the hue by 78 degrees)
hueShader.setFloat("hue", 78);
```

Below is a list of methods available for setting uniform values on a `FlxRuntimeShader`:

- `setFloat(name:String, value:Float)`
- `setFloatArray(name:String, value:Array<Float>)`
- `setInt(name:String, value:Int)`
- `setIntArray(name:String, value:Array<Int>)`
- `setBool(name:String, value:Bool)`
- `setBoolArray(name:String, value:Array<Bool>)`
- `setBitmapData(name:String, value:openfl.display.BitmapData)`

There are also corresponding `get` methods for each of these to retrieve the current uniform values.

## Custom Shader Classes

For more complex shaders, you might want to create a custom shader class that extends `FlxRuntimeShader`. Here's an example of how to create a custom shader class:

```haxe
import flixel.addons.display.FlxRuntimeShader;

class HueShiftShader extends FlxRuntimeShader
{
    // notice how we have a function that sets the shader's hue uniform when we set the hue variable
    public var hue(default, set):Float = 0;

    public function new()
    {
        super(Assets.getText(Paths.frag('hueShift')));
        hue = 0;
    }

    function set_hue(value:Float):Float
    {
        this.setFloat('hue', value);
        this.hue = value;

        return this.hue;
    }
}
```

You can then use this custom shader class in your stage script like so:

```haxe
hueShader = new HueShiftShader();
hueShader.hue = 120; // Set the hue shift to 120 degrees
PlayState.instance.currentStage.getBoyfriend().shader = hueShader;
```

> Author: [BobbyDX](https://github.com/bobbydeluxe)